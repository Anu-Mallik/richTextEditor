apply plugin: 'maven-publish'
apply plugin: 'signing'

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

tasks.register('androidSourcesJar', Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                tasks.named("generateMetadataFileForReleasePublication").configure { dependsOn("androidSourcesJar") }

                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    artifact("$buildDir/libs/${project.getName()}-${version}.jar")
                }

                // artifact androidSourcesJar
                // Mostly self-explanatory metadata
                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Android WYSIWYG Rich editor for Jetpack compose.'
                    url = 'https://github.com/canopas/rich-editor-compose'
                    licenses {
                        license {
                            name = 'License'
                            url = 'https://github.com/canopas/rich-editor-compose/blob/main/License'
                        }
                    }
                    developers {
                        developer {
                            id = 'cp-radhika-s'
                            name = 'Radhika canopas'
                            email = 'radhika.s@canopas.com'
                        }
                        // Add all other devs here...
                    }

                    // Version control info - if you're using GitHub, follow the
                    // format as seen here
                    scm {
                        connection = 'scm:git:github.com/canopas/rich-editor-compose.git'
                        developerConnection = 'scm:git:ssh://github.com/canopas/rich-editor-compose.git'
                        url = 'https://github.com/canopas/rich-editor-compose.git'
                    }
                }
            }
        }
    }
}
signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )

    sign publishing.publications
}